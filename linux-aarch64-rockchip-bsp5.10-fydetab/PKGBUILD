# Maintainer: Samuel (kwankiu)

_kernel=linux-aarch64-rockchip-bsp5.10-fydetab
pkgver=5.10.110.37.27a257394
pkgbase=$_kernel
pkgname=($pkgbase)
pkgrel=1
arch=('aarch64')
license=('GPL2')
options=('!strip')
makedepends=('binutils' 'tar' 'grep' 'mkinitcpio' 'sed')
pkgdesc="Precompiled FydeTab Rockchip BSP 5.10 Linux kernel package"

source=("https://github.com/kwankiu/archlinux-installer/releases/download/kernel/fydetab-duo-kernel-bin.tar.xz")
sha256sums=('SKIP')

_package() {
  pkgdesc="FydeTab Rockchip BSP 5.10 Linux kernel"
  depends=('coreutils' 'linux-firmware' 'kmod' 'grep' 'gzip')
  optdepends=('crda: to set the correct wireless channels of your country')
  provides=("linux=${pkgver}")
  conflicts=('linux')

  cd "$srcdir"

  local _preversion="5.10.110-fyde"

  install -dm755 "$pkgdir/boot"
  install -dm755 "$pkgdir/usr"
  
  # copy dtbs
  mkdir -p "$pkgdir/boot/dtbs/$_kernel"
  cp -r boot/rockchip "$pkgdir/boot/dtbs/$_kernel/rockchip"

  # copy modules and firmware
  mkdir -p "$pkgdir/usr/lib"
  cp -r usr/lib/modules "$pkgdir/usr/lib/modules"
  cp -r usr/lib/firmware "$pkgdir/usr/lib/firmware"

  # copy kernel
  cp -r boot/Image "$pkgdir/usr/lib/modules/$_preversion/vmlinuz"

  # used by mkinitcpio to name the kernel
  echo "$_kernel" | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules/$_preversion/pkgbase"

}

for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$_kernel}")
    _package${_p#$_kernel}
  }"
done