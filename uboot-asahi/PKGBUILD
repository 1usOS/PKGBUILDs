# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2024.07
_asahirel=1

pkgver=${_ver}.asahi${_asahirel}
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('69d27ecfa676e12c8333126122a968e08f107e48d93de7ea58d176a977413675')
b2sums=('83a9f6b3df87bdd54f3bd9ca0ba2e0b12fe6ef2f35eee9a2f9ba72ebcfd06d63f000c9da7c6ff87681c78c15a8b647dc97d5f13c2be64fe3693895f0996ce6d7')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
