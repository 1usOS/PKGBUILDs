# Maintainer: Samuel (kwankiu)
# Maintainer: Nikita Almakov <nikita.almakov@gmail.com>

pkgname=rate-mirrors
pkgver=0.18.0
pkgrel=1
pkgdesc="Everyday-use client-side map-aware mirror ranking tool"
url="https://github.com/westandskif/rate-mirrors"
license=('custom')
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/westandskif/${pkgname}/archive/v${pkgver}.tar.gz"
        "0001-custom-mirrorlist-url.patch")
sha256sums=('be1985ab978b46a1eeba03379f28d62e27d28c170ed6091a339f80ba7ffc85d6'
            '8ead13c9978fa80c3ff0742a9c8eaa559a93ae11fdc227d1752935a5d6346517')
arch=('x86_64' 'aarch64')
depends=('gcc-libs')
makedepends=('cargo')

build() {
  cd "$pkgname-$pkgver"

  # Replace archarm mirrorlist url
  patch -p1 -i ../0001-custom-mirrorlist-url.patch

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "target/release/rate_mirrors" "$pkgdir/usr/bin/${pkgname}"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
