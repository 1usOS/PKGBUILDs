pkgname=tiny-dfr
pkgver=0.3.2
pkgrel=1
pkgdesc="The most basic dynamic function row daemon possible"
arch=('aarch64' 'x86_64')
url="https://github.com/WhatAmISupposedToPutHere/tiny-dfr"
license=(MIT)
makedepends=(cargo git libinput)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/WhatAmISupposedToPutHere/tiny-dfr/archive/refs/tags/v${pkgver}.tar.gz"
)
sha256sums=(
  'a70128dd04bc1dfba778f3756a89a3c36eb398c0d0f1604e0b47f8ef62b8d4af'
)

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --locked --offline --release --all-features
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -r share/* "$pkgdir/usr/share"
  mkdir "$pkgdir/etc"
  cp -r etc/* "$pkgdir/etc"
}
